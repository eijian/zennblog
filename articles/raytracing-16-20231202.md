---
title: "レイトレーシング(16): 光源あれこれ（前編）"
emoji: "💡"
type: "tech" # tech: 技術記事 / idea: アイデア
topics: ["Haskell", "RayTracing", "PhotonMapping"]
published: false
---

# 0. はじめに

これまでの実装は光源に関していろいろ妥協した部分があり、いくつか問題がある。以下にそれを
列挙する。

* 物理モデルに反している
    - 現実世界では発光体の表面から放射される光子は面法線との角度が大きくなるに従い減少するが
      （余弦則という）、どの角度に同比率で放射されている。
* 物体と光源が別のオブジェクトとして定義されている
    - 光源体を直接見ることができない、自由な形の光源が作れない
* 自然な光源色（たとえば太陽光、蛍光灯、など）の指定が難しい
    - 日常では光源の色は色温度、強さはルーメン、などで指定しているが実装はRGB比率で色を、強さは放射束で指定するため変換が大変。

そこで2回に分けてこれらの問題を解消するための改良について記事にしたいと思う。

## Light型の再定義

改良の説明に入る前に、理解の助けとしてこれまでと新しい光源の定義をそれぞれ示す。増減・変化した要素について
この後説明していく。なお、従来は光源と物体は別のオブジェクトだったのを改良後は「物体に付与する属性」というふうに
変更したため、型名を"Light"から"LightSpec"に変更している。

従来の光源型(Light)は以下の通り3種類の光源の列挙型であった。

```Haskell:src/Ray/Light.hs
data Light =
  PointLight                      -- 点光源
  { lcolor :: Color                 -- RGBの比率、r+g+b = 1.0
  , lflux  :: Flux                  -- 放射束 (W)
  , pos    :: Position3
  }
  |
  ParallelogramLight              -- 面光源
  { lcolor :: Color                 -- RGBの比率、r+g+b = 1.0
  , lflux  :: Flux                  -- 放射束 (W)
  , pos    :: Position3
  , nvec   :: Direction3
  , dir1   :: Direction3
  , dir2   :: Direction3
  }
  |
  SunLight                        -- 平行光源（太陽光）
  { lcolor :: Color                 -- RGBの比率、r+g+b = 1.0
  , lflux  :: Flux                  -- 放射束 (W)
  , pos    :: Position3
  , nvec   :: Direction3
  , dir1   :: Direction3
  , dir2   :: Direction3
  , ldir   :: Direction3            -- 光線の方向
  }
```

新しい光源定義は「光源の仕様」とした。

```Haskell:src/Ray/Light.hs
data LightSpec = LightSpec
  { lcolor      :: !Color         -- RGBの比率、r+g+b = 1.0
  , radiosity   :: !Radiosity     -- 放射発散度 [W/m^2]
  , directivity :: !Double        -- 指向性 (-1.0:指向性なし 〜 1.0:平行光源)
  , radest      :: !RadEstimation -- 直接光の輝度値計算方法
  , dirflag     :: !InOut         -- 発光方向（通常光源=Out, ドーム光源等=In)
  -- calcuration when initializing
  , cospower    :: !Double
  , power       :: !Double
  , emittance0  :: !Radiance      -- 放射輝度（面法線方向）
  }
  deriving (Eq, Show, Generic)
```

このように、従来に比べパラメータがかなり増えている。一方で点光源、面光源などの光源の種類はなくした。

# 1. 物理モデルに基づく放射

問題点の1つ目は、従来の実装では「光の放出が物理モデルに基づいていない」ことであった。

従来の実装では、面光源から放出される光子の量（光度）は方向によらず同じとしていた。

$$
I_θ = I_0 \quad(ただしI_0は面法線方向の光度)
$$

一方、フォトンマッピングの本() p.72 5.1.2には放出される光子の量は余弦則に従うとあり、以下の式に従う。

$$
\begin{align}
  I_θ = I_0 \cos \theta
\end{align}
$$

図にすると以下の通りとなる。本来は面との角度に応じて水平になる程、その方向へは光子が放出されないのだ。

（図：光の放出量（a. 従来の実装、b. 物理モデル）

実は最初から認識はしていたが実装はできていなかった。なぜなら、余弦則に合うように放出される光子の量を
制御する方法が分からなかったからだ。式で書くと簡単だが、実際フォトン追跡において角度45°の方向には
垂直方向の$\frac{1}{\sqrt 2}$の量の光子を放出する必要がある。どうやって？

しかしそれも今は昔。もうその方法は知っている。第14回で光沢面の実装の際に調べた計算式が今回も使えるのだ。
面法線になす角$\theta$の方向に分散する光の量が$\cos \theta$に比例する場合、ランダムに放射される光の方向ベクトル`v`は
$\xi_1$, $\xi_2$を0〜1の一様乱数とすれば以下の式で表される。

$$
v = [x, y, z] \quad where
$$

$$
\begin{align}
x &= \cos (2\pi \xi_1) \sqrt{1-\xi_2} \\
y &= \sqrt{\xi_2} \\
z &= \sin (2\pi \xi_1) \sqrt{1-\xi_2}
\end{align}
$$

$$
    ※ yが法線方向
$$

それでは放出する光子の方向ベクトル$v$を求めているソースコードの部分を従来実装と改良版で比べてみよう。

従来の実装を以下に示す。

```Haskell:src/Ray/Light.hs
generatePhoton :: Light -> IO Photon
generatePhoton (ParallelogramLight c _ p n d1 d2) = do      -- 平面光源のコード
  wl <- MT.randomIO :: IO Double
  t1 <- MT.randomIO :: IO Double
  t2 <- MT.randomIO :: IO Double
  d  <- diffuseReflection n
  let r = initRay (p + t1 *> d1 + t2 *> d2) d
      w = decideWavelength c wl
  return (w, r)
```

```Haskell:src/Ray/Optics.hs
diffuseReflection :: Direction3 -> IO Direction3
diffuseReflection n = do
  dir <- generateRandomDir4                                -- 360°ランダムな方向のベクトルを乱数で生成し、
  let c = n <.> dir                                        -- 法線ベクトルnとのなす角が45°に収まるよう処置
  return $ if c > 0.0 then dir else negate dir
```

diffuseReflection関数では、引数`n`で与えた法線ベクトルとランダムに生成した方向ベクトルとが90°以内の角度に
収まるよう補正をしている。よって、どの方向にも等確率で光子を放出することになる。

一方改良版での実装は以下の通り。

```Haskell:src/Ray/Light.hs
generatePhoton :: LightSpec -> SurfacePoint -> IO (Photon, RadEstimation)
generatePhoton (LightSpec col _ _ radest dirf _ pow _) (pos, nvec) = do
  wl <- MT.randomIO :: IO Double
  let
    nvec2 = if dirf == Out
      then nvec
      else negate nvec
  nvec' <- blurredVector nvec2 pow
  return ((decideWavelength col wl, initRay pos nvec'), radest)
```

```Haskell:src/Ray/Algebra.hs
blurredVector :: Direction3 -> Double -> IO Direction3
blurredVector nvec pow = do
  xi1 <- MT.randomIO :: IO Double    -- horizontal
  xi2 <- MT.randomIO :: IO Double    -- virtical
  let
    phi = 2.0 * pi * xi2
    uvec0 = normalize $ nvec <*> (Vector3 0.00424 1.0 0.00764)
    uvec = case uvec0 of
      Just v  -> v
      Nothing -> fromJust $ normalize $ nvec <*> (Vector3 1.0 0.00424 0.00764)
    vvec = uvec <*> nvec
    xi1' = xi1 ** pow
    rt = sqrt (1.0 - xi1' * xi1')

    x = cos phi * rt
    y = xi1'
    z = sin phi * rt

    nvec' = x *> uvec + y *> nvec + z *> vvec
  case normalize nvec' of
    Just v  -> return v
    Nothing -> return ex3    
```

`blurredVector`が処理の実体である。ここでは前述の数式に従い、2つの一様乱数`xi1`および
`xi2`を使って光子の放出される方向を`nvec'`として求める。途中、`xi1`を`pow`乗しているが、
詳しいことは後の"指向性光源"で述べよう。いまは`pow=0.5(=1/2)`が入っていると考えてもらいたい。
それはつまり`xi1`の$\frac{1}{2}$乗（＝$\sqrt{\xi_1}$）を求めることになる。ここで求めた`x`、`y`、`z`を成分とするベクトルを面の法線ベクトル方向に傾けることで、光子の放射方向を決定する。

両者のベクトルの求め方を図にすると以下のようになる。

（図：従来と改良後のベクトルの求め方模式図）

実際にどのような違いとなるのか、サンプル画像を比べてみよう。

|従来プログラム|改良版プログラム|
|:---:|:---:|
|![図2-a:従来プログラムでのサンプルシーン](/images/raytracing-16/scene-n16-m1.0-diff.png =320x)|![図2-b:改良版プログラムでのサンプルシーン](/images/raytracing-16/scene-n16-0.0-diff.png =320x)|

*図2: 従来と改良版の比較*


両方の画像の赤丸部分を見て欲しい。
従来は天井の面光源の光子が水平方向にも同じ量だけ放出されているため、側壁の天井との境目が最も明るくなっている。
一方改良版では水平方向に近づくほど光子の放出量が減るため、同じ部分はそれほど明るくなく、少し下の方が最も明るくなっているのがわかる。
また、改良版のほうが少し床が明るい。これは、より多くの光子が床面に向けて放射されるからである。

## 2. 指向性光源

1では面法線との角度$\theta$の余弦に比例して光子の放射量を増減させていた。実はこの角度に応じた
放射量の制御には一般的な形が存在し、それは$\cos \theta$の$n$乗で表されるのだ。

$$
\begin{align}
  I_θ = I_0 \cos^n \theta
\end{align}
$$

これに基づき放射方向$x, y, z$をランダムに決めるための式は次の通りである。
これは第14回でも示したものだ。

$$
\begin{align}
x &= \cos(2\pi\xi_1) \sqrt{1-\xi_2^{\frac{2}{n+1}}} \\
y &= \xi_2^{\frac{1}{n+1}} \\
z &= \sin(2\pi\xi_1) \sqrt{1-\xi_2^{\frac{2}{n+1}}}
\end{align}
$$

$n$が大きいほど光子の放射方向が面法線に偏ることになる。グラフで表すと次のようになる。

（cos^nのグラフ）

$n = 0$では角度によらず一定であるから、これは従来版のように全方向に同じ量の光子を放出
することになる。$n = 1$では1節で書いたように一般的な光源を表す。では$n$がそれ以上では
何かというと、これはいわゆる"指向性光源"である。特に$n = \infty$のとき、光子は面法線方向
のみに放出されるので、いわゆる平行光源がこれで表現できるのだ。よって、従来は点光源や
平行光源をそれぞれ定義していたが、これらはこの指向性光源を応用することで作ることが
できる。

> 点光源： 極小の球形で$n = \infty$の指向性光源（球形の光源は次回で検討する）。
> 平行光源：平面で$n = \infty$の志向性光源

ではこれらをどのようにパラメータで与えるようにしたかを説明する。以下がその`LightSpec`型の
定義であった（再掲）。

```Haskell:src/Ray/Light.hs
data LightSpec = LightSpec
  { lcolor      :: !Color         -- RGBの比率、r+g+b = 1.0
  , radiosity   :: !Radiosity     -- 放射発散度 [W/m^2]
  , directivity :: !Double        -- 指向性 (-1.0:指向性なし 〜 1.0:平行光源)
  , radest      :: !RadEstimation -- 直接光の輝度値計算方法
  , dirflag     :: !InOut         -- 発光方向（通常光源=Out, ドーム光源等=In)
  -- calcuration when initializing
  , cospower    :: !Double
  , power       :: !Double
  , emittance0  :: !Radiance      -- 放射輝度（面法線方向）
  }
  deriving (Eq, Show, Generic)
```

`directivity`が指向性度合いを意味するパラメータである。範囲は$-1.0 \sim 1.0$である。この値を
変換して$0 \leq n \leq \infty$となるような変換式が必要だ。そこで`directivity`を$d$として、次の
ような変換式を用いることにした。

$$
\begin{align}
n &= d + 1 & (d \leq 0.0) \\
n &= 3^p & (d > 0.0)
\end{align}
$$

ただし、 

<!-- 案1: p &= \frac{1}{0.05} \left(1 - \sqrt{1 - 2 \left(0.65 d \right) ^2} \right) \\ -->
<!--        &= 20 \left(1 - \sqrt{1 - \frac{169}{200} d^2 } \right)                     -->
<!-- 案2: p &= 13.3 - \sqrt{200 - 84.5 \left( d + 0.53 \right) ^2 } -->

$$
\begin{align}
p &= 13.3 - \sqrt{200 - 84.5 \left( d + 0.53 \right) ^2 }
\end{align}
$$

である。
なぜこんな面倒な式になっているかというと、特に参考情報があるわけではなく`directivity`を
$-1.0 \sim 1.0$で変化させた時に生成される画像で光源の指向具合がそれなりに滑らか（線形）に
なる関数が欲しかったから。

この$p$の複雑な式がどこからきているかというと、試行錯誤の末に次の楕円の$0 \leq d \leq 1$の
範囲の曲線が良さげな関数として浮上したからだ。

<!-- 楕円式 案1：2 \left( 0.65 d \right) ^2 + \left( 0.05 p - 1 \right) ^2 = 1 -->
<!-- 楕円式 案2：169 \left( d + 0.53 \right) ^2 + 2 \left( p - 13.3 \right) ^2 = 400 -->

$$
\begin{align}
169 \left( d + 0.53 \right) ^2 + 2 \left( p - 13.3 \right) ^2 = 400
\end{align}
$$

（楕円グラフ）

これを$p$について解いたものが式(11)というわけ。
とはいえ完全に納得できているわけではないので、もっと良い関数を今後も模索するかもしれない。
下表は`directivity`と、「理想の$n$」および「計算値$n$」を並べたものである。

|directivity|理想の$n$|計算値$n$|光源の特性|
|:---:|:---:|:---:|:---:|
|$-1.0$ | $0$ | $0$| 全方向に一様に放射 |
|$0.0$ | $1$ | $1$ | 一般的な光源 |
|$0.5$ | ? | $21.6$ | スポッ

トライト |
|$1.0$ | $\infty$ | 約$440,000$ | 平行光源 |

なお、`LightSpec`型のパラメータのうち、`cospower`と`power`についても触れておく。これらは
最初に与えるものではなく、`directivity`から計算して求める。`cospower`が余弦の冪乗、すなわち
式5における$n$を保持するもの、さらに実際の光子の放出方向を求める際には$\frac{1}{n+1}$が何度も必要に
なるため(式6〜8)、これもあらかじめ計算しておき`power`に保持するようにした。

0.1: 0.3980
0.2: 0.8513
0.3: 1.3925
0.4: 2.0343
0.5: 2.7951
0.6: 3.7030
0.7: 4.8053
0.8: 6.1917
0.9: 8.0841
1.0: 11.8188


### コーディング

この部分のコードを見てみよう。

```Haskell:src/Ray/Light.hs
initLightSpec :: Color -> Radiosity -> Double -> RadEstimation -> InOut -> LightSpec
initLightSpec col lux direct radest dirf = LightSpec col radiosity direct radest dirf cpow pow em
  where
    radiosity = lux / 683.0 
    (cpow, pow)  = densityPower2 direct     -- cpow=n, pow=1/(n+1) をあらかじめ計算して保持しておく
    em0 = sr_half * radiosity
    em  = em0 *> col <**> radiance1
```

```Haskell:src/Ray/Algebra.hs
densityPower :: Double -> (Double, Double)
densityPower r = (n, 1.0 / (n + 1.0))
  where
    r' = if r > 1.0
      then 1.0
      else
        if r < (-1.0)
          then (-1.0)
          else r
    n = 10.0 ** (6.0 * r')
```



・・・

### 作例

下記画像はいずれもイテレーションあたりのフォトン数20,000、イテレーション2000回、解像度は320ドットx240ドットである。


|directivity|n|生成画像|
|:---:|:---:|:---:|
|-1.0|0.00|![サンプルシーン(n=-1.0)](/images/raytracing-16/scene-n16-m1.0.png =320x)|
|-0.5|0.50|![サンプルシーン(n=-0.5)](/images/raytracing-16/scene-n16-m0.5.png =320x)|
| 0.0|1.00|![サンプルシーン(n=0.0)](/images/raytracing-16/scene-n16-0.0.png =320x)|
| 0.2|2.55|![サンプルシーン(n=0.2)](/images/raytracing-16/scene-n16-0.2.png =320x)|
| 0.4|9.35|![サンプルシーン(n=0.4)](/images/raytracing-16/scene-n16-0.4.png =320x)|
| 0.6|58.45|![サンプルシーン(n=0.6)](/images/raytracing-16/scene-n16-0.6.png =320x)|
| 0.8|900.19|![サンプルシーン(n=0.8)](/images/raytracing-16/scene-n16-0.8.png =320x)|
| 1.0|43,5608.26|![サンプルシーン(n=1.0)](/images/raytracing-16/scene-n16-1.0.png =320x)|

# 3. まとめ




---

